#!/usr/bin/env tclsh

## Load Private Dependencies
##  - Use explicit sourcing here to avoid environment hack with normal installation
#####################

set managerHome [file normalize [file dirname [info script]]/../]

#### ODFI Dev TCL
if {![file exists $managerHome/private/odfi-dev-tcl/]} {

	puts "
# Private Dependency ODFI dev tcl is missing.
# Trying to clone using command line call -> This is the only way to solve that for now.
# It this fails, simply install the master version there: $managerHome/private/odfi-dev-tcl
"
    catch {puts [exec git clone http://lebleu/gitlab/odfi/odfi-dev-tcl.git  $managerHome/private/odfi-dev-tcl]}

}


if {[catch {
	source $managerHome/private/odfi-dev-tcl/tcl/common.tm
	source $managerHome/private/odfi-dev-tcl/tcl/git-1.0.0.tm
	source $managerHome/private/odfi-dev-tcl/tcl/closures-2.0.0.tm
	source $managerHome/private/odfi-dev-tcl/tcl/list-2.0.0.tm
	source $managerHome/private/odfi-dev-tcl/tcl/tool-1.0.0.tm
	source $managerHome/private/odfi-dev-tcl/tcl/os-1.0.0.tm
} res]} {

	## An error occured here, exit
	#######
	set host [info hostname]

	## Output a message, or none if we are on a known incompatible host
	puts "# Current host $host is not compatible with odfi, probably missing TCL packages like ITCL"
	exit 0
}


## Local definitions
source $managerHome/private/module.tm

## Load ODFI configuration (config and installed modules)
####################
::new odfi::manager::ODFI odfi.local $managerHome



## Go on
################
odfi::tool::describe {

	set description "

Odfi Module Manager:

	- Install/Update module
	- Load modules to environment
"

	argument info optional description "Provide some general infos about installation"

	argument list optional description "List available modules, versions and installed one"

	argument load optional description "Load installation"

	argument install type string optional description "Install or update a module"

	argument update optional description "Tries to Update all the modules"

	argument setup type string optional description "Call setup on specified module name"

	argument remove type string optional description "Removes a local module"

	argument switch-url type list size 2 optional description "Use as --switch-url installed_module_name url_name to change repository url"

	argument clone optional description "Clone"


}

tool parseArguments


#### Info
##########################
if {$info} {

	## Print General Infos
	###########################
	odfi.local printInfos


	## Show Resolved list
	##############################
	odfi::common::println "- Resolved installed modules list for load:"

	odfi::common::printlnIndent
	foreach installedModule [odfi.local resolveInstalledModules] {


		odfi::common::println "- $installedModule"

	}
	odfi::common::printlnOutdent

	exit 1
}



#### Update
#####################
if {$update} {

	## Update private dependencies
	odfi::git::pull $managerHome/private/odfi-dev-tcl --rebase

	## Update Ourselves
	odfi::git::pull $managerHome --rebase

	odfi::common::println "Updating modules"
	odfi::common::printlnIndent
	foreach installedModule [itcl::find objects -class odfi::manager::InstalledModule] {

		odfi::common::println "- [$installedModule name]"
		$installedModule doUpdate

	}
	odfi::common::printlnOutdent


	exit 1

}

#### Install
#####################
if {$install!=false} {

	odfi::common::println "Installing module: $install"

	## Check argument validity
	###########

	set moduleDefinition [lindex [itcl::find objects "$install" -class odfi::manager::Module] 0]

	if {$moduleDefinition==""} {

		odfi::common::println "Module $install is not present in config file..."
		exit 0
	}




	## Update/Install
	#####################
	$moduleDefinition update


	exit 1

}


#### Setup
#####################
if {$setup!=false} {

	odfi::common::println "Setup module: $setup"

	## Check argument validity
	###########
	set moduleDefinition [lindex [itcl::find objects "$setup" -class odfi::manager::Module] 0]

	if {$moduleDefinition==""} {

		odfi::common::println "Module $setup is not present in config file..."
		exit 0
	}
	if {![$moduleDefinition isInstalled]} {

		odfi::common::println "Module $setup is not installed..."
		exit 0
	}

	[$moduleDefinition getInstalledModule] doSetup


	exit 1

}

#### Remove
####################
if {$remove != false} {

	odfi::common::println "Remove module: $remove"

	## Check argument validity
	###########
	set moduleDefinition [lindex [itcl::find objects "$remove" -class odfi::manager::Module] 0]

	if {$moduleDefinition==""} {

		odfi::common::println "Module $remove is not present in config file..."
		exit 0
	}

	if {![$moduleDefinition isInstalled]} {

		odfi::common::println "Module $remove is not installed..."
		exit 0
	}

	[$moduleDefinition getInstalledModule] remove


	exit 1

}

#### Load
#####################
if {$load} {

	puts "#Loading ODFI"
	odfi::manager::LoadResult loadResult

	## Add The manager to bin
	###################
	loadResult env PATH $managerHome/bin

	## Load Modules
	##############################""


	foreach installedModule [odfi.local resolveInstalledModules] {

		## Call Load on module
		################
		$installedModule doLoad loadResult

	}

	## Output result to underlying command line
	############
	odfi::os::onOs {
		linux* {
			puts "[loadResult toBash]"
		}
	}



	exit 1
}


#### Switch URL
#########################
if {${switch-url}!=false} {

	set project [lindex ${switch-url} 0]
	set url 	[lindex ${switch-url} 1]

	odfi::common::println "Requested Switch URL for $project, to url $url"

	## Check Project exists
	###########
	set moduleDefinition [lindex [itcl::find objects "$project" -class odfi::manager::Module] 0]

	if {$moduleDefinition==""} {

		odfi::common::println "Module $project is not present in config file..."
		exit 0
	}

	if {![$moduleDefinition isInstalled]} {

		odfi::common::println "Module $project is not installed..."
		exit 0
	}

	## Check URL exists
	###############
	if {![$moduleDefinition hasUrl $url]} {

		odfi::common::println "Requested url $url is not defined..."
		exit 0
	}

	[$moduleDefinition getInstalledModule] switch-url $url [$moduleDefinition url $url]


	exit 1

}

#### Clone
####################
if {$clone} {

	puts "
WARNING: This will duplicate this module manager to specified location
This feature is intended for developer who want to locally override a module installation
"

	## Prepare location
	###########################
	set location [pwd]/modules-manager
	puts "Clone location: $location"

	## Ask
	puts "Do you want to continue ? (y/n)"
	switch -glob -- [gets stdin] {

		yes -
		y {

			## Get origin
			set cloneURL $managerHome

			## Cloning
			puts ""
			puts "Cloning Manager from $cloneURL"

			odfi::git::clone $cloneURL $location

			## Create config file for parent
			set parentConfigFile [open $location/odfi.parent.config "w+"]
			puts $parentConfigFile "
## Config from clone operation to set source manager from $managerHome as parent
parent central $managerHome
"

			## Boostrap
			odfi::os::onOs {
				linux* {
					puts "Calling setup once on the cloned manager to ensure correct setup"
					puts [exec bash $location/setup.linux.bash]
				}
			}

		}
		default {
			puts "Aborting."
		}
	}


	exit 1

}



tool usage
